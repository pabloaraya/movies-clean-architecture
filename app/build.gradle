apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

apply from: './jacoco.gradle'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "pabloaraya.org.moviesparty"
    minSdkVersion 23
    targetSdkVersion 28
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  signingConfigs {
    release {
      storeFile file(System.env.KEYSTORE_FILE ?: '<missing>')
      storePassword System.env.KEYSTORE_PSW
      keyAlias System.env.KEYALIAS_NAME
      keyPassword System.env.KEYALIAS_PSW
    }
  }

  buildTypes {
    release {
      debuggable false
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      minifyEnabled false
      debuggable false
      testCoverageEnabled true
    }
  }

  lintOptions {
    abortOnError false
  }

  checkstyle {
    toolVersion "8.4"
  }

  task checkstyle(type: Checkstyle) {
    description 'Run checkstyle'
    group 'verification'
    configFile file("${project.rootDir}/configs/checkstyle/GoogleJavaStyle.xml")
    configProperties = [rootDir: "${project.rootDir}"]
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation "androidx.recyclerview:recyclerview:1.0.0"
  implementation "androidx.cardview:cardview:1.0.0"

  implementation 'com.google.android.material:material:1.0.0'

  implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
  implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

  implementation 'com.airbnb.android:lottie:2.8.0'
  implementation 'com.squareup.picasso:picasso:2.71828'

  implementation "com.google.dagger:dagger:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:runner:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

  implementation project(':presentation')
}
